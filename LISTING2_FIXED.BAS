10 PRINT "MAMMOTH MAZE in 2.5K BYTES BY G.T.RELF"
20 REM SET STARTING POSITION (EAST, WEST, DOWN):
30 X=103 : Y=97 : Z=1
40 REM SET THREASHOLD
50 T=0.3
60 REM RANDOMIZE FOR EARTHQUAKES
70 RANDOM
80 REM INSERT ANY OTHER INITIALISING STATEMENTS HERE.
90 REM MAIN LOOP:
100 REM COMPUTE PROPERTIES OF CURRENT LOCATION
110 GOSUB 250
120 REM GET COMMAND & BRANCH ACCORDINGLY:
130 INPUT "COMMAND"; CM$
140 CM$=LEFT(CM$,1)
150 IF CM$="N" THEN 490
160 IF CM$="E" THEN 530
170 IF CM$="W" THEN 570
180 IF CM$="S" THEN 610
190 IF CM$="U" THEN 650
200 IF CM$="D" THEN 690
210 PRINT "IMPOSSIBLE!"
220 GOTO 110
230 REM END OF MAIN LOOP
240 REM COMPUTE ATTRIBUTES OF CURRENT LOCATION:
245 REM FIXED: ADD CALL TO RANDOM NUMBER GENERATOR AT BEGINNING
250 GOSUB 720
255 V1=INT(W*1000)
260 IF V1>35 AND V1<39 AND Z>1 THEN PRINT "HERE IS A CROCK OF GOLD!"
270 REM (YOU WONT FIND CROCKS OF GOLD JUST LYING ON THE GROUND!)
280 REM INSERT OTHER ATTRIBUTE TESTS HERE.
290 REM CHECK FOR EARTHQUAKES:
300 IF RND(1000)=9 THEN PRINT "RUMBLE, RUMBLE..." : T=T+0.2*RND(0)-0.1
310 REM INSERT OTHER LOCATION-DEPENDANT TESTS HERE.
320 REM IDENTIFY CORRIDORS FROM CURRENT LOCATION (CHANGES W):
330 IF Z=1 THEN 440
340 PRINT "CORRIDORS: "; : NC=0
350 X=X+1: GOSUB 720 : IF W<T THEN PRINT "E "; : NC=NC+1
360 X=X-2 : GOSUB 720 : X=X+1 : IF W<T THEN PRINT "W "; : NC=NC+1
370 Y=Y+1: GOSUB 720 : IF W<T THEN PRINT "N "; : NC=NC+1
380 Y=Y-2 : GOSUB 720 : Y=Y+1 : IF W<T THEN PRINT "S "; : NC=NC+1
390 Z=Z+1: GOSUB 720 : IF W<T THEN PRINT "D "; : NC=NC+1
400 Z=Z-2 : GOSUB 720 : Z=Z+1 : IF W<T OR Z=2 THEN PRINT "U" : NC=NC+1
410 IF NC=0 THEN PRINT "NONE - THE EARTHQUAKE HAS TRAPPED YOU!"
420 PRINT
430 RETURN
440 PRINT "GROUND LEVEL."
450 Z=Z+1 : GOSUB 720 : Z=Z-1
460 IF W<T THEN PRINT "A SHAFT DESCENDS FROM HERE."
470 RETURN
480 REM MOVE NORTH:
490 Y=Y+1 : IF Z=1 THEN 110
500 GOSUB 720 : IF W<T THEN 110
510 Y=Y-1 : GOTO 210
520 REM MOVE EAST:
530 X=X+1 : IF Z=1 THEN 110
540 GOSUB 720 : IF W<T THEN 110
550 X=X-1 : GOTO 210
560 REM MOVE WEST:
570 X=X-1 : IF Z=1 THEN 110
580 GOSUB 720 : IF W<T THEN 110
590 X=X+1 : GOTO 210
600 REM MOVE SOUTH:
610 Y=Y-1 : IF Z=1 THEN 110
620 GOSUB 720 : IF W<T THEN 110
630 Y=Y+1 : GOTO 210
640 REM MOVE UP:
650 IF Z=1 THEN 210
655 Z=Z-1 : IF Z=1 THEN 110
660 GOSUB 720 : IF W<T THEN 110
670 Z=Z+1 : GOTO 210
680 REM MOVE DOWN:
690 Z=Z+1 : GOSUB 720 : IF W<T THEN 110
700 Z=Z-1 : GOTO 210 
710 REM RANDOM FUNCTION
720 U=100*SQR(X*X+Y*Y*Z)
730 W=U-INT(U)
740 RETURN
